class GoogleMaps.Custom.Map
  # _.extend @prototype, Backbone.Events

  zoom: 15

  constructor: (lat, lng, options={})->
    @vent = options.vent
    @_buildMap(lat, lng)
    @_setHomeMarker(lat, lng)

  setView: (latitude, longitude)->
    @map.panTo new google.maps.LatLng(latitude, longitude)
    @map.setZoom @zoom
    @_setHomeMarker(latitude, longitude)

  createMarker: (latitude, longitude, options={})->
    marker_options = _.defaults(options,
      position: new google.maps.LatLng(latitude, longitude)
    )
    marker = new google.maps.Marker(marker_options)
    marker

  showMarker: (marker)->
    marker.setMap(@map)
    @_mapBounds().extend(marker.getPosition())

  removeMarker: (marker)->
    marker.setMap(null)
    marker = null

  bindPopup: (marker, content)->
    infowindow = new google.maps.InfoWindow(content: content)
    google.maps.event.addListener(marker, 'click', ->
      infowindow.open(@map, marker)
    )

  _buildMap: (latitude, longitude)->
    @map = new google.maps.Map(document.getElementById('map-canvas'),
      center: new google.maps.LatLng(latitude, longitude)
      zoom: 15
    )

  _setHomeMarker: (latitude, longitude)->
    @removeMarker(@home_marker) if @home_marker?
    home_marker = @createMarker(latitude, longitude, draggable: true)
    @showMarker(home_marker)
    @_setHomeMarkerListeners(home_marker)
    @home_marker = home_marker

  _setHomeMarkerListeners: (home_marker)->
    that = this
    map = @map
    infowindow = new google.maps.InfoWindow(content: "You are here")
    google.maps.event.addListener(home_marker, 'click', ->
      infowindow.open(map, home_marker)
    )
    google.maps.event.addListener(home_marker, 'dragend', ->
      pos = home_marker.getPosition()
      that.vent.trigger 'Map:home_marker:dragend', pos.lat(), pos.lng()
    )

  _createNumericIcon: (number)->
    img_path = "assets/map_icons/number_#{number}.png"
    { url: img_path }

  _mapBounds: ()->
    @__mapBounds ?= new google.maps.LatLngBounds